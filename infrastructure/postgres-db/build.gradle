import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    id 'info.solidsoft.pitest'
    id 'org.springframework.boot' apply(false)
    id 'nu.studer.jooq' version '6.0.1'
}

pitest {
    testPlugin = "junit5"
    targetClasses = ['pp.ua.lifebook.db.*']
    verbose = true
}

dependencies {
    implementation project(':domain')

    implementation platform(SpringBootPlugin.BOM_COORDINATES)
    implementation "org.springframework:spring-context-support"

    implementation libs.logging
    implementation 'com.google.guava:guava:19.0'
    implementation 'org.apache.commons:commons-lang3:3.4'
    implementation 'org.apache.commons:commons-dbcp2:2.1.1'
    implementation 'org.apache.velocity:velocity:1.7'
    implementation "org.springframework:spring-jdbc"
    implementation 'org.postgresql:postgresql:42.3.8'
    implementation "org.flywaydb:flyway-core:${flyway_version}"
    implementation 'org.json:json:20230227'

    jooqGenerator 'org.postgresql:postgresql:42.3.8'
    implementation "org.jooq:jooq:${jooq_version}"
    implementation "org.jooq:jooq-codegen:${jooq_version}"
    implementation "org.jooq:jooq-meta:${jooq_version}"

    testImplementation "org.junit.jupiter:junit-jupiter:${junit5_version}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit5_version}"

    // Testcontainers for integration testing
    testImplementation libs.testcontainers
    testImplementation "org.testcontainers:postgresql:${testcontainersVersion}"

    // AssertJ for fluent assertions
    testImplementation libs.assertJ

    // Include db-migrations for Flyway migrations
    testImplementation project(':db-migrations')
}

ext['jooq.version'] = "${jooq_version}"

jooq {
    version = "${jooq_version}"

    configurations {
        main {  // name of the jOOQ configuration
            generateSchemaSourceOnCompilation = false

            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = 'jdbc:postgresql://localhost:5432/lifebook'
                    user = 'postgres'
                    password = '123'
                    properties {
                        property {
                            key = 'ssl'
                            value = 'false'
                        }
                    }
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        forcedTypes {
                            forcedType {
                                name = 'varchar'
                                includeExpression = '.*'
                                includeTypes = 'JSONB?'
                            }
                            forcedType {
                                name = 'varchar'
                                includeExpression = '.*'
                                includeTypes = 'INET'
                            }
                        }
                        excludes = '''
                            flyway_schema_history
                        '''
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                        pojosEqualsAndHashCode = true
                    }
                    target {
                        packageName = 'pp.ua.lifebook.storage.db.scheme'
                        directory = 'generated-src/jooq/main'  // default (can be omitted)
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}
